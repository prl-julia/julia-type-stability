#!/usr/bin/env julia
using CSV, DataFrames, Statistics, StatsBase

# Script to generate Tables 1 and 2 of the paper
# Input: file "report.csv" generated by `scripts/pkgs-report.sh` in the working dir
# Ouput: terminal

df=CSV.File("report.csv") |> DataFrame

# Computes the ratio n/d if denominator isn't 0
getRatio(n, d) = d == 0 ? 0 : n / d

df[!, "Inst/Meth"]    = round.(getRatio.(df.Instances, df.Methods), digits=1)

df[!, "Varargs (%)"]  = round.(Int, getRatio.(df.vararg,   df.Methods) .* 100)
df[!, "Stable (%)"]   = round.(Int, getRatio.(df.stable,   df.Instances) .* 100)
df[!, "Grounded (%)"] = round.(Int, getRatio.(df.grounded, df.Instances) .* 100)

select!(df, Not([:stable ,:grounded, :nospec, :vararg, :Fail]))

# stats for Table 1
c = nrow(df) > 1 # don't use Bessel's correction for std dev if only one row is present
stst = round.(Int, mean_and_std(df[!, "Stable (%)"]; corrected=c))
grst = round.(Int, mean_and_std(df[!, "Grounded (%)"]; corrected=c))
df1  = DataFrame(
       :Stats    => ["Mean",  "Median",                      "Std. Dev."],
       :Stable   => [stst[1], median(df[!, "Stable (%)"]),   stst[2]],
       :Grounded => [grst[1], median(df[!, "Grounded (%)"]), grst[2]] )

println("Table 1")
println(df1)
println()

df2 = first(df, 10) 
sort!(df2)
println("Table 2")
println(df2)

